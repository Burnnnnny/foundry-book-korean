## Config

`Config` 추상 컨트랙트는 테스트 및 스크립트에서 멀티체인 환경을 위한 구성을 관리하는 간소화된 방법을 제공합니다. TOML 구성 파일을 자동으로 로드 및 파싱하고, 지정된 체인에 대한 포크를 생성하며, 구성 변수에 대한 타입 안전(type-safe) 접근을 제공합니다.

### Import

```solidity
import {Config} from "forge-std/Config.sol";
```

### Usage

`Test`(또는 `Script`)와 `Config`를 모두 상속받습니다:

```solidity
contract MyTest is Test, Config {
    function setUp() public {
        // Load config and create forks for all chains
        _loadConfigAndForks("./config.toml", false);
    }
}
```

### Storage Variables

`Config` 컨트랙트는 다음 스토리지 변수를 제공합니다:

- **`config`**: 모든 구성 데이터를 보유하는 `StdConfig` 인스턴스
- **`chainIds`**: 포크가 생성된 체인 ID 배열
- **`forkOf`**: 간편한 포크 전환을 위한 체인 ID에서 포크 ID로의 매핑

### Functions

#### `_loadConfig`

TOML 파일에서 구성을 로드합니다.

```solidity
function _loadConfig(string memory filePath, bool writeToFile) internal
```

**Parameters:**
- `filePath`: TOML 구성 파일의 경로
- `writeToFile`: 업데이트 내용을 TOML 파일에 다시 기록할지 여부

#### `_loadConfigAndForks`

구성을 로드하고 구성에 지정된 각 체인에 대한 포크를 생성합니다.

```solidity
function _loadConfigAndForks(string memory filePath, bool writeToFile) internal
```

**Parameters:**
- `filePath`: TOML 구성 파일의 경로
- `writeToFile`: 업데이트 내용을 TOML 파일에 다시 기록할지 여부 (스크립팅 시에만 가능)

### Configuration File Format

TOML 구성 파일은 다음 구조를 따라야 합니다:

```toml
# Using Alloy chain aliases (must match exactly) or numberic chain IDs (always works)
# See https://github.com/alloy-rs/chains for supported aliases
[mainnet]
endpoint_url = "${MAINNET_RPC}"

[mainnet.bool]
is_live = true

[mainnet.address]
weth = "0xc02aaa39b223fe8d0a0e5c4f27ead9083c756cc2"
admins = ["0x1234...", "${ADMIN_ADDRESS}"]

[mainnet.uint]
important_number = 123
numbers = [456, 789]

[optimism]
endpoint_url = "${OPTIMISM_RPC}"

[optimism.address]
weth = "0x4200000000000000000000000000000000000006"
```

#### Chain Identifiers

최상위 체인 키는 다음 중 하나여야 합니다:
- **숫자 체인 ID**: `[1]`, `[10]`, `[11155111]` - 커스텀 네트워크를 포함한 모든 체인에서 작동합니다.
- **Alloy 체인 별칭**: `[mainnet]`, `[optimism]`, `[sepolia]` - [Alloy 체인](https://github.com/alloy-rs/chains/blob/main/src/named.rs)의 정확한 별칭과 일치해야 합니다.

:::warning
`my_local_chain`과 같은 커스텀 체인 이름은 Alloy 체인에 존재하지 않는 경우 `InvalidChainKey` 에러를 발생시킵니다. 새 네트워크나 커스텀 네트워크의 경우 체인 ID를 사용하세요.
:::

**Key points:**
- 최상위 키는 체인 식별자(이름 또는 ID)를 나타냅니다.
- 각 체인 아래에서 변수는 `bool`, `address`, `uint`, `int`, `bytes32`, `string`, `bytes` 타입별로 구성됩니다.
- 환경 변수는 자동으로 확인됩니다(예: `${MAINNET_RPC}`).
- 모든 타입에 대해 배열이 지원됩니다.

### Example: Multi-chain Testing

```solidity
contract MultiChainTest is Test, Config {
    function setUp() public {
        _loadConfigAndForks("./config.toml", false);
    }

    function test_readValues() public {
        // Switch to mainnet fork
        vm.selectFork(forkOf[1]);

        // Read mainnet WETH address
        address wethMainnet = config.get("weth").toAddress();

        // Switch to optimism fork
        vm.selectFork(forkOf[10]);

        // Read optimism WETH address
        address wethOptimism = config.get("weth").toAddress();

        // Values are chain-specific
        assertEq(wethMainnet, 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);
        assertEq(wethOptimism, 0x4200000000000000000000000000000000000006);
    }
}
```

### Example: Configuration Updates

```solidity
contract DeploymentScript is Script, Config {
    function run() public {
        // Enable writing updates back to file
        _loadConfig("./deployments.toml", true);

        vm.startBroadcast();

        // Deploy contract
        MyContract deployed = new MyContract();

        // Save deployment address to config
        config.set("my_contract", address(deployed));
        config.set("deployed_at", block.timestamp);

        vm.stopBroadcast();

        // Changes are automatically persisted to deployments.toml
    }
}
```

### See Also

- [StdConfig](/reference/forge-std/std-config.mdx) - 기본 구성 컨트랙트
- [Scripting with Config](/guides/scripting-with-config.md) - Config를 사용한 스크립트 오케스트레이션 가이드
