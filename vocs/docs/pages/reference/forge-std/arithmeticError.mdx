## `arithmeticError`

### 시그니처

```solidity
stdError.arithmeticError
```

### 설명

산술 연산이 실패할 때(예: 언더플로우 및 오버플로우) 발생하는 내부 Solidity 오류입니다.

### 예제

일부 토큰(`wmdToken`)을 저장할 수 있는 기본 금고(vault) 컨트랙트가 있다고 가정해 봅시다:

```solidity
contract BasicVault {

    IERC20 public immutable wmdToken;   
    mapping(address => uint) public balances;

    event Deposited(address indexed from, uint amount);
    event Withdrawal(address indexed from, uint amount);

    constructor(IERC20 wmdToken_){
        wmdToken = wmdToken_;
    }

    function deposit(uint amount) external {    
        balances[msg.sender] += amount;
        bool success = wmdToken.transferFrom(msg.sender, address(this), amount);
        require(success, "Deposit failed!"); 
        emit Deposited(msg.sender, amount);
    }

    function withdraw(uint amount) external {      
        balances[msg.sender] -= amount;
        bool success = wmdToken.transfer(msg.sender, amount);
        require(success, "Withdrawal failed!");
        emit Withdrawal(msg.sender, amount);
    }
}
```

사용자가 예치금을 초과하여 토큰을 인출할 수 없도록 보장하는 테스트 함수가 있습니다:

```solidity
function testUserCannotWithdrawExcessOfDeposit() public {
    vm.prank(user);
    vm.expectRevert(stdError.arithmeticError);
    vault.withdraw(userTokens + 100*10**18);
}
```

1. 사용자가 금고 컨트랙트에 `userTokens` 금액의 토큰을 예치했습니다.
2. 사용자가 예치금을 초과하는 금액의 토큰을 인출하려고 시도합니다.
3. 이는 `balances[msg.sender] -= amount;`에서 음수 값이 되므로 언더플로우 오류로 이어집니다.

"Arithmetic over/underflow" 오류를 포착하기 위해 언더플로우가 발생할 것으로 예상되는 함수 호출 바로 앞에 `vm.expectRevert(stdError.arithmeticError)`를 삽입합니다.
