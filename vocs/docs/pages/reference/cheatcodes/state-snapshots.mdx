## `snapshotState` 치트코드

### 시그니처

```solidity
/// 현재 EVM 상태의 스냅샷을 찍습니다.
/// 생성된 스냅샷의 ID를 반환합니다.
/// 스냅샷을 되돌리려면 `revertToState`를 사용하세요.
function snapshotState() external returns (uint256 snapshotId);

/// EVM 상태를 이전 스냅샷으로 되돌립니다.
/// 되돌릴 스냅샷 ID를 받습니다.
/// 스냅샷이 성공적으로 되돌려지면 `true`를 반환합니다.
/// 스냅샷이 존재하지 않으면 `false`를 반환합니다.
/// **참고:** 이것은 스냅샷을 자동으로 삭제하지 않습니다. 스냅샷을 삭제하려면 `deleteStateSnapshot`을 사용하세요.
function revertToState(uint256 snapshotId) external returns (bool success);

/// EVM 상태를 이전 스냅샷으로 되돌리고 자동으로 스냅샷을 삭제합니다.
/// 되돌릴 스냅샷 ID를 받습니다.
/// 스냅샷이 성공적으로 되돌려지고 삭제되면 `true`를 반환합니다.
/// 스냅샷이 존재하지 않으면 `false`를 반환합니다.
function revertToStateAndDelete(uint256 snapshotId) external returns (bool success);

/// `snapshot`에 의해 생성된 주어진 ID의 스냅샷을 제거합니다.
/// 삭제할 스냅샷 ID를 받습니다.
/// 스냅샷이 성공적으로 삭제되면 `true`를 반환합니다.
/// 스냅샷이 존재하지 않으면 `false`를 반환합니다.
function deleteStateSnapshot(uint256 snapshotId) external returns (bool success);

/// `snapshot`에 의해 이전에 생성된 _모든_ 스냅샷을 제거합니다.
function deleteStateSnapshots() external;
```

### 설명

`snapshotState`는 블록체인 상태의 스냅샷을 찍고 생성된 스냅샷의 식별자를 반환합니다.

`revertToState`는 블록체인의 상태를 주어진 상태 스냅샷으로 되돌립니다. 이는 주어진 스냅샷과 그 이후에 찍은 모든 스냅샷을 삭제합니다 (예: id 2로 되돌리면 id 2, 3, 4 등의 스냅샷이 삭제됩니다).

### 예제

```solidity
struct Storage {
    uint slot0;
    uint slot1;
}

contract SnapshotStateTest is Test {
    Storage store;
    uint256 timestamp;

    function setUp() public {
        store.slot0 = 10;
        store.slot1 = 20;
        vm.deal(address(this), 5 ether);            // balance = 5 ether
        timestamp = block.timestamp;
    }

    function testSnapshotState() public {
        uint256 snapshot = vm.snapshotState();      // 상태 저장

        // 상태 변경
        store.slot0 = 300;
        store.slot1 = 400;
        vm.deal(address(this), 500 ether);
        vm.warp(12345);                             // block.timestamp = 12345

        assertEq(store.slot0, 300);
        assertEq(store.slot1, 400);
        assertEq(address(this).balance, 500 ether);
        assertEq(block.timestamp, 12345);

        vm.revertToState(snapshot);                 // 상태 복원

        assertEq(store.slot0, 10, "snapshot revert for slot 0 unsuccessful");
        assertEq(store.slot1, 20, "snapshot revert for slot 1 unsuccessful");
        assertEq(address(this).balance, 5 ether, "snapshot revert for balance unsuccessful");
        assertEq(block.timestamp, timestamp, "snapshot revert for timestamp unsuccessful");
    }
}
```
