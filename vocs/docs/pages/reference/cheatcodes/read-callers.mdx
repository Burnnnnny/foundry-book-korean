## `readCallers`

### 시그니처

```solidity
enum CallerMode {
    None,
    Broadcast,
    RecurrentBroadcast,
    Prank,
    RecurrentPrank
}

function readCallers()
external
returns (CallerMode callerMode, address msgSender, address txOrigin);
```

### 설명

현재 `CallerMode`, `msg.sender`, `tx.origin`을 읽습니다.

`CallerMode` 열거형은 활성화된 호출자 수정이 있는지 여부와 그 유형을 나타냅니다.

- 활성화된 프랭크(prank)가 있는 경우:

  - `callerMode`는 다음과 같습니다:
    - 프랭크가 [`prank`](/reference/cheatcodes/prank.mdx)로 설정된 경우 `CallerMode.Prank`.
    - 프랭크가 [`startPrank`](/reference/cheatcodes/start-prank.mdx)로 설정된 경우 `CallerMode.RecurrentPrank`.

- 활성화된 브로드캐스트가 있는 경우:

  - `callerMode`는 다음과 같습니다:
    - 브로드캐스트가 [`broadcast`](/reference/cheatcodes/broadcast.mdx)로 설정된 경우 `CallerMode.Broadcast`.
    - 브로드캐스트가 [`startBroadcast`](/reference/cheatcodes/start-broadcast.mdx)로 설정된 경우 `CallerMode.RecurrentBroadcast`.

- 활성화된 호출자 수정이 없는 경우:
  - `callerMode`는 `CallerMode.None`입니다.

### 예제

```solidity
CallerMode callerMode;
address msgSender;
address txOrigin;

// 예제 1
(callerMode, msgSender, txOrigin) = vm.readCallers();
assertEq(callerMode, CallerMode.None);
assertEq(msgSender, defaultSenderAddress);
assertEq(txOrigin, defaultOriginAddress);

// 예제 2
vm.prank(senderPrankAddress);
(callerMode, msgSender, txOrigin) = vm.readCallers();
assertEq(callerMode, CallerMode.Prank);
assertEq(msgSender, senderPrankAddress);
assertEq(txOrigin, defaultOriginAddress);

// 예제 3
vm.prank(senderPrankAddress, originPrankAddress);
(callerMode, msgSender, txOrigin) = vm.readCallers();
assertEq(callerMode, CallerMode.Prank);
assertEq(msgSender, senderPrankAddress);
assertEq(txOrigin, originPrankAddress);

// 예제 4
vm.broadcast(broadcastAddress);
(callerMode, msgSender, txOrigin) = vm.readCallers();
assertEq(callerMode, CallerMode.Broadcast);
assertEq(msgSender, broadcastAddress);
assertEq(txOrigin, broadcastAddress);
```

### 같이 보기

- [prank](/reference/cheatcodes/prank.mdx)
- [startPrank](/reference/cheatcodes/start-prank.mdx)
- [broadcast](/reference/cheatcodes/broadcast.mdx)
- [startBroadcast](/reference/cheatcodes/start-broadcast.mdx)
