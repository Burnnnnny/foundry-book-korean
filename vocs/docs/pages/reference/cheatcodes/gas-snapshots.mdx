## `snapshotGas` 치트코드

### 서명

```solidity
/// 이름으로 현재 가스 사용량의 스냅샷 캡처를 시작합니다.
/// 그룹 이름은 컨트랙트 이름에서 파생됩니다.
function startSnapshotGas(string calldata name) external;

/// 그룹 내의 이름으로 현재 가스 사용량의 스냅샷 캡처를 시작합니다.
function startSnapshotGas(string calldata group, string calldata name) external;

/// 최신 스냅샷 이름으로 현재 가스의 스냅샷 캡처를 중지하고 시작 이후 사용된 가스를 캡처합니다.
function stopSnapshotGas() external returns (uint256 gasUsed);

/// 이름으로 현재 가스 사용량의 스냅샷 캡처를 중지하고 시작 이후 사용된 가스를 캡처합니다.
/// 그룹 이름은 컨트랙트 이름에서 파생됩니다.
function stopSnapshotGas(string calldata name) external returns (uint256 gasUsed);

/// 그룹 내의 이름으로 현재 가스 사용량의 스냅샷 캡처를 중지하고 시작 이후 사용된 가스를 캡처합니다.
function stopSnapshotGas(string calldata group, string calldata name) external returns (uint256 gasUsed);

/// 임의의 숫자 값을 이름으로 스냅샷 캡처합니다.
/// 그룹 이름은 컨트랙트 이름에서 파생됩니다.
function snapshotValue(string calldata name, uint256 value) external;

/// 그룹 내의 이름으로 임의의 숫자 값을 스냅샷 캡처합니다.
function snapshotValue(string calldata group, string calldata name, uint256 value) external;

/// 피호출자 관점에서 마지막 호출의 가스 사용량을 이름으로 스냅샷 캡처합니다.
function snapshotGasLastCall(string calldata name) external returns (uint256 gasUsed);

/// 피호출자 관점에서 그룹 내의 이름으로 마지막 호출의 가스 사용량을 스냅샷 캡처합니다.
function snapshotGasLastCall(string calldata group, string calldata name) external returns (uint256 gasUsed);
```

### 설명

`snapshotGas*` 치트코드를 사용하면 테스트에서 가스 사용량을 캡처할 수 있습니다. 이는 로직이 소비하는 가스의 양을 추적하는 데 유용할 수 있습니다. 이름으로 마지막 호출의 가스 사용량을 캡처하거나, 이름으로 임의의 숫자 값을 캡처하거나, 이름으로 현재 가스 사용량의 스냅샷 캡처를 시작하고 중지할 수 있습니다.

테스트 실행 간에 가스 사용량을 엄격하게 비교하려면 다음 중 하나를 설정할 수 있습니다:

- `FORGE_SNAPSHOT_CHECK=true` 환경 변수 설정
- `foundry.toml`에서 `gas_snapshot_check = true` 설정
- `--gas-snapshot-check=true` 전달

기본적으로 이것은 **활성화되지 않으며** `--gas-snapshot-check=false`를 전달하면 다른 모든 설정보다 우선합니다.

이렇게 하면 테스트의 가스 사용량을 마지막 스냅샷과 비교하고 가스 사용량이 변경되면 실패합니다.

`snapshotGas*` 치트코드를 사용할 때 생성된 `snapshots` 디렉토리는 버전 관리에 체크인하는 것이 좋습니다. 이를 통해 시간 경과에 따른 가스 사용량 변화를 추적하고 코드 검토 중에 가스 사용량을 비교할 수 있습니다.

가스 스냅샷 방출을 비활성화하려면 다음 중 하나를 설정할 수 있습니다:

- `FORGE_SNAPSHOT_EMIT=false` 환경 변수 설정
- `foundry.toml`에서 `gas_snapshot_emit = false` 설정
- `--gas-snapshot-emit=false` 전달

기본적으로 이것은 **활성화되어 있으며** 가스 스냅샷이 디스크에 기록됩니다.

> ℹ️ **격리된 테스트 (Isolated tests)**
>
> 이 치트코드는 격리된 테스트 모드를 사용하지 않는 경우 정확하지 않습니다.
>
> `--isolate` 플래그를 전달하여 격리 모드를 활성화하거나 다음 인라인 구성으로 테스트 함수에 태그를 지정할 수 있습니다:
>
> `/// forge-config: default.isolate = true`

### 예제

외부 컨트랙트를 호출하는 코드 섹션의 가스 사용량 캡처:

```solidity
contract SnapshotGasTest is Test {
    Flare public flare;

    function setUp() public {
        flare = new Flare();
    }

    function testSnapshotGas() public {
        vm.startSnapshotGas("externalA");
        flare.run(256);
        uint256 gasUsed = vm.stopSnapshotGas();
    }
}
```

내부 상태를 수정하는 여러 코드 섹션의 가스 사용량 캡처:

```solidity
contract SnapshotGasTest is Test {
    uint256 public slot0;

    /// `snapshots/SnapshotGasTest.json` 그룹에 `internalA`, `internalB`, `internalC` 이름으로 씁니다.
    function testSnapshotGas() public {
        vm.startSnapshotGas("internalA");
        slot0 = 1;
        vm.stopSnapshotGas();

        vm.startSnapshotGas("internalB");
        slot0 = 2;
        vm.stopSnapshotGas();

        vm.startSnapshotGas("internalC");
        slot0 = 0;
        vm.stopSnapshotGas();
    }
}
```

내부 상태를 수정하고 외부 컨트랙트를 호출하는 코드 섹션의 가스 사용량 캡처:

```solidity
contract SnapshotGasTest is Test {
    uint256 public slot0;
    Flare public flare;

    function setUp() public {
        flare = new Flare();
    }

    /// `snapshots/SnapshotGasTest.json` 그룹에 `combinedA` 이름으로 씁니다.
    function testSnapshotGas() public {
        vm.startSnapshotGas("combinedA");
        flare.run(256);
        slot0 = 1;
        vm.stopSnapshotGas();
    }
}
```

임의의 숫자 값(예: 컨트랙트의 바이트코드 크기) 캡처:

```solidity
contract SnapshotGasTest is Test {
    uint256 public slot0;

    /// `snapshots/SnapshotGasTest.json` 그룹에 `valueA`, `valueB`, `valueC` 이름으로 씁니다.
    function testSnapshotValue() public {
        uint256 a = 123;
        uint256 b = 456;
        uint256 c = 789;

        vm.snapshotValue("valueA", a);
        vm.snapshotValue("valueB", b);
        vm.snapshotValue("valueC", c);
    }
}
```

피호출자 관점에서 마지막 호출의 가스 사용량 캡처:

```solidity
contract SnapshotGasTest is Test {
    Flare public flare;

    function setUp() public {
        flare = new Flare();
    }

    /// `snapshots/SnapshotGasTest.json` 그룹에 `lastCallA` 이름으로 씁니다.
    function testSnapshotGasLastCall() public {
        flare.run(1);
        vm.snapshotGasLastCall("lastCallA");
    }
}
```

위의 각 예제에 대해 치트코드의 `group` 변형을 사용하여 스냅샷을 사용자 정의 그룹으로 그룹화할 수도 있습니다.

```solidity
contract SnapshotGasTest is Test {
    uint256 public slot0;

    /// `snapshots/CustomGroup.json` 그룹에 `internalA`, `internalB`, `internalC` 이름으로 씁니다.
    function testSnapshotGas() public {
        vm.startSnapshotGas("CustomGroup", "internalA");
        slot0 = 1;
        vm.stopSnapshotGas();

        vm.startSnapshotGas("CustomGroup", "internalB");
        slot0 = 2;
        vm.stopSnapshotGas();

        vm.startSnapshotGas("CustomGroup", "internalC");
        slot0 = 0;
        vm.stopSnapshotGas();
    }
}
```
