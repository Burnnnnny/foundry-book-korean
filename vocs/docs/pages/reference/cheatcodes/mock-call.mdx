## `mockCall`

### 시그니처

```solidity
function mockCall(address where, bytes calldata data, bytes calldata retdata) external;
```

```solidity
function mockCall(
    address where,
    uint256 value,
    bytes calldata data,
    bytes calldata retdata
) external;
```

### 설명

호출 데이터가 `data`와 엄격하게 또는 느슨하게 일치하고 `retdata`를 반환하면 `where` 주소에 대한 모든 호출을 모의(Mock)합니다.

`where`에 대한 호출이 이루어지면 먼저 호출 데이터가 `data`와 전체적으로 일치하는지 확인합니다.
그렇지 않은 경우 호출 데이터의 첫 번째 바이트부터 시작하여 부분 일치가 있는지 확인합니다.

일치하는 항목이 발견되면 호출에서 `retdata`가 반환됩니다.

**두 번째 시그니처를 사용하면** 특정 `msg.value`로 호출을 모의할 수 있습니다. 모호한 경우 `Calldata` 일치가 `msg.value`보다 우선합니다.

모의 호출은 [`clearMockedCalls`](/reference/cheatcodes/clear-mocked-calls.mdx)가 호출될 때까지 유효합니다.

> ℹ️ **참고**
>
> 주소에 코드가 없으면 모의 주소에 대한 호출이 되돌려질(Revert) 수 있습니다.
> 이는 Solidity가 일부 컨트랙트 호출 전에 `extcodesize` 검사를 삽입하기 때문입니다.
>
> 이를 우회하려면 모의 주소에 코드가 없는 경우 [`etch`](/reference/cheatcodes/etch.mdx) 치트코드를 사용하세요.

> ℹ️ **내부 호출 (Internal calls)**
>
> 이 치트코드는 현재 내부 호출에서 작동하지 않습니다. 문제 [#432](https://github.com/foundry-rs/foundry/issues/432)를 참조하세요.

### 예제

정확한 호출 모의:

```solidity
function testMockCall() public {
    vm.mockCall(
        address(0),
        abi.encodeWithSelector(MyToken.balanceOf.selector, address(1)),
        abi.encode(10)
    );
    assertEq(IERC20(address(0)).balanceOf(address(1)), 10);
}
```

전체 함수 모의:

```solidity
function testMockCall() public {
    vm.mockCall(
        address(0),
        abi.encodeWithSelector(MyToken.balanceOf.selector),
        abi.encode(10)
    );
    assertEq(IERC20(address(0)).balanceOf(address(1)), 10);
    assertEq(IERC20(address(0)).balanceOf(address(2)), 10);
}
```

주어진 `msg.value`로 호출 모의:

```solidity
function testMockCall() public {
    assertEq(example.pay{value: 10}(1), 1);
    assertEq(example.pay{value: 1}(2), 2);
    vm.mockCall(
        address(example),
        10,
        abi.encodeWithSelector(example.pay.selector),
        abi.encode(99)
    );
    assertEq(example.pay{value: 10}(1), 99);
    assertEq(example.pay{value: 1}(2), 2);
}
```

Public 변수 모의:

```solidity
contract Example {
    uint256 public number = 10;
}

contract ExampleTest is Test {
    Example public example;

    function setUp() public {
        example = new Example();
    }

    function testMockPublicVariable() public {
        assertEq(example.number(), 10);
        vm.mockCall(
            address(example),
            abi.encodeWithSelector(bytes4(keccak256("number()"))),
            abi.encode(5)
        );
        assertEq(example.number(), 5);
    }
}
```
