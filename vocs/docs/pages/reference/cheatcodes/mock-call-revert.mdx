## `mockCallRevert`

### 시그니처

```solidity
function mockCallRevert(address where, bytes calldata data, bytes calldata retdata) external;
```

```solidity
function mockCallRevert(
    address where,
    uint256 value,
    bytes calldata data,
    bytes calldata retdata
) external;
```

### 설명

호출 데이터가 `data`와 엄격하게 또는 느슨하게 일치하고 `retdata`를 반환하면 `where` 주소에 대한 모든 호출을 되돌립니다(Revert).

`retdata`는 원시 반환 메시지이거나 사용자 정의 오류일 수 있습니다.

`where`에 대한 호출이 이루어지면 먼저 호출 데이터가 `data`와 전체적으로 일치하는지 확인합니다.
그렇지 않은 경우 호출 데이터의 첫 번째 바이트부터 시작하여 부분 일치가 있는지 확인합니다.

일치하는 항목이 발견되면 호출이 되돌려지고 `retdata`가 반환됩니다.

**두 번째 시그니처를 사용하면** 특정 `msg.value`로 호출을 모의할 수 있습니다. 모호한 경우 `Calldata` 일치가 `msg.value`보다 우선합니다.

되돌려진 모의 호출은 [`clearMockedCalls`](/reference/cheatcodes/clear-mocked-calls.mdx)가 호출될 때까지 유효합니다.

> ℹ️ **내부 호출 (Internal calls)**
>
> 이 치트코드는 현재 내부 호출에서 작동하지 않습니다. 문제 [#432](https://github.com/foundry-rs/foundry/issues/432)를 참조하세요.

### 예제

원시 오류 메시지로 정확한 호출 되돌리기:

```solidity
function testMockCallRevert() public {
    vm.mockCallRevert(
        address(0),
        abi.encodeWithSelector(MyToken.balanceOf.selector, address(1)),
        "REVERT_MESSAGE"
    );
    vm.expectRevert("REVERT_MESSAGE");
    IERC20(address(0)).balanceOf(address(1));
}
```

사용자 정의 오류로 호출 되돌리기:

```solidity
function testMockCallRevertWithCustomError() public {
    bytes memory customError = abi.encodeWithSelector(TestError.selector, "ERROR_MESSAGE");
    vm.mockCallRevert(
        address(0),
        abi.encodeWithSelector(MyToken.balanceOf.selector, address(1)),
        customError
    );
    vm.expectRevert(customError);
    IERC20(address(0)).balanceOf(address(1));
}
```

주어진 `msg.value`로 호출 모의:

```solidity
function testMockCallRevertWithValue() public {
    assertEq(example.pay{value: 10}(1), 1);
    assertEq(example.pay{value: 1}(2), 2);
    vm.mockCallRevert(
        address(example),
        10,
        abi.encodeWithSelector(example.pay.selector),
        "ERROR_MESSAGE"
    );
    assertEq(example.pay{value: 1}(2), 2);
    vm.expectRevert("ERROR_MESSAGE");
    assertEq(example.pay{value: 10}(1), 99);
}
```
