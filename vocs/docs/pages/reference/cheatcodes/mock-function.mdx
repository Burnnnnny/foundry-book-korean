## `mockFunction`

### 시그니처

```solidity
function mockFunction(address callee, address target, bytes calldata data) external;
```

### 설명

호출 데이터가 `data`와 엄격하게 또는 느슨하게 일치하는 경우 `target` 주소의 바이트코드를 사용하여 `callee` 주소에 대한 호출을 실행합니다.

`callee`에 대한 호출이 이루어지면 먼저 호출 데이터가 `data`와 전체적으로 일치하는지 확인합니다.
그렇지 않은 경우 호출 데이터의 함수 선택자(function selector)에서 부분 일치가 있는지 확인합니다.

일치하는 항목이 발견되면 `target` 주소의 바이트코드를 사용하여 호출이 실행됩니다.

> ℹ️ **격리된 테스트 (Isolated tests)**
>
> 이 치트코드는 현재 격리된 테스트 모드를 사용하는 경우 작동하지 않습니다.

### 예제

두 컨트랙트(동일한 스토리지 레이아웃을 가짐)의 경우:
```solidity
contract Counter {
    uint256 public a;

    function count(uint256 x) public {
        a = 321 + x;
    }
}

contract ModelCounter {
    uint256 public a;

    function count(uint256 x) public {
        a = 123 + x;
    }
}
```
`count` 함수에 대한 정확한 호출 모의:

```solidity
function testMockFunction() public {
    vm.mockFunction(
        address(counter),
        address(model),
        abi.encodeWithSelector(Counter.count.selector, 456)
    );
    counter.count(456);
    assertEq(counter.a(), 123 + 456);
    counter.count(567);
    assertEq(counter.a(), 321 + 567);
}
```

`count` 함수에 대한 모든 호출 모의:

```solidity
function testMockCall() public {
    vm.mockFunction(
        address(counter),
        address(model),
        abi.encodeWithSelector(Counter.count.selector)
    );
    counter.count(678);
    assertEq(counter.a(), 123 + 678);
    counter.count(789);
    assertEq(counter.a(), 123 + 789);
}
```
