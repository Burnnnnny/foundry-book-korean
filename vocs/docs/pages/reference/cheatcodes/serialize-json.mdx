## `serializeJson`

### 시그니처

```solidity
function serializeJson(string calldata objectKey, string calldata value)
    external
    returns (string memory json);

function serializeBool(string calldata objectKey, string calldata valueKey, bool value)
    external
    returns (string memory json);

function serializeUint(string calldata objectKey, string calldata valueKey, uint256 value)
    external
    returns (string memory json);

function serializeInt(string calldata objectKey, string calldata valueKey, int256 value)
    external
    returns (string memory json);

function serializeAddress(string calldata objectKey, string calldata valueKey, address value)
    external
    returns (string memory json);

function serializeBytes32(string calldata objectKey, string calldata valueKey, bytes32 value)
    external
    returns (string memory json);

function serializeString(string calldata objectKey, string calldata valueKey, string calldata value)
    external
    returns (string memory json);

function serializeBytes(string calldata objectKey, string calldata valueKey, bytes calldata value)
    external
    returns (string memory json);

function serializeBool(string calldata objectKey, string calldata valueKey, bool[] calldata values)
    external
    returns (string memory json);

function serializeUint(string calldata objectKey, string calldata valueKey, uint256[] calldata values)
    external
    returns (string memory json);

function serializeInt(string calldata objectKey, string calldata valueKey, int256[] calldata values)
    external
    returns (string memory json);

function serializeAddress(string calldata objectKey, string calldata valueKey, address[] calldata values)
    external
    returns (string memory json);

function serializeBytes32(string calldata objectKey, string calldata valueKey, bytes32[] calldata values)
    external
    returns (string memory json);

function serializeString(string calldata objectKey, string calldata valueKey, string[] calldata values)
    external
    returns (string memory json);

function serializeBytes(string calldata objectKey, string calldata valueKey, bytes[] calldata values)
    external
    returns (string memory json);
```

### 설명

값을 문자열화된 JSON 객체로 직렬화합니다.

### 작동 방식

사용자가 JSON 파일의 값을 직렬화하고 마지막으로 해당 객체를 파일에 쓰는 방식입니다. 사용자는 다음을 전달해야 합니다:

- 값이 직렬화되어야 하는 _객체_에 대한 키입니다. 이를 통해 사용자는 여러 객체를 병렬로 직렬화할 수 있습니다.
- JSON 파일에서 키가 될 _값_에 대한 키입니다.
- 직렬화할 값입니다.

이에 대한 예외는 `serializeJson` 함수로, `objectKey`와 json 문자열 `value`만 받습니다. 이를 통해 사용자는 기존 json 객체를 직렬화하고 제공된 `objectKey`에 직접 할당할 수 있습니다. `objectKey`가 이미 사용 중인 경우 전체 직렬화된 json을 덮어씁니다.

키는 특정 형식이 필요하지 않습니다. 직관적인 인간 해석을 가능하게 하기 위해 `string` 타입입니다. 의미적으로는 키로 사용되는 것 외에는 중요하지 않습니다.

치트코드는 **해당 시점까지** 직렬화된 JSON 객체를 반환합니다. 이렇게 하면 사용자는 내부 JSON 객체를 직렬화한 다음 더 큰 JSON 객체로 직렬화하여 임의의 JSON 객체를 만들 수 있습니다.

마지막으로 사용자는 [writeJson](/reference/cheatcodes/write-json.mdx)을 사용하여 JSON 객체를 JSON 파일에 씁니다.
또는 [writeToml](/reference/cheatcodes/write-toml.mdx)을 사용하여 JSON 객체를 TOML 파일에 쓸 수 있습니다.

**기억하세요:** 파일 경로는 허용된 경로에 있어야 합니다. 자세한 내용은 [파일 치트코드](/reference/cheatcodes/fs.mdx)를 읽어보세요.

### 예제

다음 JSON을 파일에 쓰고 싶다고 가정해 봅시다:

`{ "boolean": true, "number": 342, "object": { "title": "finally json serialization" } }`

```solidity
string memory obj1 = "some key";
vm.serializeBool(obj1, "boolean", true);
vm.serializeUint(obj1, "number", uint256(342));

string memory obj2 = "some other key";
string memory output = vm.serializeString(obj2, "title", "finally json serialization");

// 중요: 이것은 `serializeString`이 먼저 `output`을 문자열화된 JSON 객체로 해석하려고 시도하기 때문에 작동합니다.
//   구문 분석이 실패하면 대신 일반 문자열로 처리합니다.
//   예를 들어, '{ "ok": "asd" }'와 같은 `output`은 객체를 생성하지만,
//   '"ok": "asd" }'와 같은 `output`은 일반 문자열을 생성합니다.
string memory finalJson = vm.serializeString(obj1, "object", output);

vm.writeJson(finalJson, "./output/example.json");
```

### 같이 보기

- [writeJson](/reference/cheatcodes/write-json.mdx)
