## `startPrank`

### 시그니처

```solidity
function startPrank(address msgSender) external;
```

```solidity
function startPrank(address msgSender, bool delegateCall) external;
```

```solidity
function startPrank(address msgSender, address txOrigin) external;
```

```solidity
function startPrank(address msgSender, address txOrigin, bool delegateCall) external;
```

### 설명

- `startPrank(address msgSender)`: [`stopPrank`](/reference/cheatcodes/stop-prank.mdx)가 호출될 때까지 **이후의 모든 호출**에 대해 `msg.sender`를 설정합니다.
- `startPrank(address msgSender, address txOrigin)`: [`stopPrank`](/reference/cheatcodes/stop-prank.mdx)가 호출될 때까지 **이후의 모든 호출**에 대해 `msg.sender`와 `tx.origin`을 설정합니다.
- `startPrank(address msgSender, bool delegateCall)`: `delegateCall` 값이 `true`이면 [`stopPrank`](/reference/cheatcodes/stop-prank.mdx)가 호출될 때까지 **이후의 모든 위임 호출**에 대해 `msg.sender`를 설정합니다.
- `startPrank(address msgSender, address txOrigin, bool delegateCall)`: `delegateCall` 값이 `true`이면 [`stopPrank`](/reference/cheatcodes/stop-prank.mdx)가 호출될 때까지 **이후의 모든 위임 호출**에 대해 `msg.sender`와 `tx.origin`을 설정합니다.

> ℹ️ **참고**
>
> 위임 호출은 EOA(외부 소유 계정)에서 프랭크(prank)할 수 없습니다.

### 예제

- 위임 호출 프랭크 및 상태 변경(diff) 기록

```solidity
contract ImplementationTest {
    uint public num;
    address public sender;

    function setNum(uint _num) public {
        num = _num;
    }
}

contract ProxyTest {
    uint public num;
    address public sender;
}

contract FoundryIssue is Script {
    function run() public {
        ProxyTest proxy = new ProxyTest();
        ImplementationTest impl = new ImplementationTest();

        vm.label(address(proxy), 'proxy');
        vm.label(address(impl), 'Impl');

        uint num = 42;
        vm.startPrank(address(proxy), true);
        vm.startStateDiffRecording();
        (bool successTwo,) = address(impl).delegatecall(abi.encodeWithSignature('setNum(uint256)', num));

        VmSafe.AccountAccess[] memory accountAccesses = vm.stopAndReturnStateDiff();
        console.log('accountAccesses.kind', uint8(accountAccesses[0].kind));
        console.log('accountAccesses.accessor', vm.getLabel(accountAccesses[0].accessor));
        console.log('accountAccesses.account', vm.getLabel(accountAccesses[0].account));
        console.logBytes(accountAccesses[0].data);
    }
}
```

```bash
== Logs ==
  accountAccesses.kind 1
  accountAccesses.accessor proxy
  accountAccesses.account Impl
  0xcd16ecbf000000000000000000000000000000000000000000000000000000000000002a
```

### 같이 보기

Forge 표준 라이브러리

[`startHoax`](/reference/forge-std/startHoax.mdx), [`changePrank`](/reference/forge-std/change-prank.mdx)
